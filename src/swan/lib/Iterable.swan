Iterable::all = def>f{
for x in this: if !f(x): return false
return true
}

Iterable::any = def>f{
for x in this: if f(x): return true
return false;
}

Iterable::none = def>f: !this.any(f)

Iterable::find = def>(f,asitr){
let it = this.iterator, value
while !((value = it.next) is undefined) {
if f(value) return asitr? it : value
}}

Iterable::count = def>f{
var count = 0
for x in this: if x==f: count+=1
return count
}

Iterable::countIf = def>f{
var count = 0
for x in this: if f(x): count+=1
return count
}

Iterable::in = def>f{
for x in this: if x==f: return true
return false
}

Iterable::first = def>: for i in this: return i

Iterable::last = def>{
let val = null
for x in this: val=x
return val
}

Iterable::sorted = def>: List(this).sort
Iterable::reversed = def>: List(this).reverse

Iterable::map = def>f: f(x) for x in this
Iterable::filter = def>f: x for x in this if f(x)

Iterable::reduce = def>(f,a){
for x in this: a = a is undefined ? x : f(a,x)
return a
}

Iterable::min = def>(f=min): this.reduce(f)
Iterable::max = def>(f=max): this.reduce(f)

Iterable::dropWhile = def>f: x for x in this while f(x)
Iterable::skipWhile = def>f: x for x in this continue while f(x)

Iterable::skip = def>n: def*{
let i = 0
for x in this {
if i>=n: yield x
i+=1
}}

Iterable::limit = def>n: def*{
let i=0
for x in this {
if i>=n: break
i+=1
yield x
}}

Iterable::enumerate = def>(n=0): def*{
for x in this {
yield (n, x)
n+=1
}}
